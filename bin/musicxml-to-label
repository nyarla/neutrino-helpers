#!/usr/bin/env perl

use strict;
use warnings;
use utf8;

our $VERSION = "v0.1.0";

use Getopt::Long
  qw( :config posix_default no_ignore_case bundling auto_help auto_version );
use Pod::Usage qw( pod2usage );
use File::Temp qw( tempdir );

my $prefix = $ENV{'NEUTRINO_PREFIX'} // $ENV{'HOME'} . '/.local/share/neutrino';

sub mkdir_p ($) {
    my $path = shift;
    return -d $path || system( qw(mkdir -p), $path ) == 0;
}

sub err($) {
    my $msg = shift;
    print STDERR ( $msg, "\n" );
    exit 1;
}

sub main {
    local @ARGV = @_;

    if ( @ARGV == 0 ) {
        pod2usage( -exitval => 0, -verbose => 1 );
    }

    my $score = q{};
    my $out   = q{};

    GetOptions(
        "score|musicxml|s|x=s" => \$score,
        "out|output|o=s"       => \$out,
        "help|h"    => sub { pod2usage( -exitval => 0, -verbose => 1 ); },
        "version|v" =>
          sub { print( ( split q{/}, $0 )[-1], " - ", $VERSION, "\n" ); exit 0 }
        ,
    ) or pod2usage( -exitval => 1, -verbose => 1 );

    if ( $score eq q{} ) {
          err "argument `score` is empty.";
    }

    if ( !-e $score ) {
          err "argument `score` (${score}) does not exists";
    }

    if ( -d $score ) {
          err "argument `score` is not file. it looks like directory?";
    }

    if ( $out eq q{} ) {
          err "argument `out` is empty.";
    }

    if ( -e $out && !-d $out ) {
          err "output directory already exists as file";
    }

    if ( !-d $out ) {
        mkdir_p($out);
    }

    my $tmpdir = tempdir( CLEANUP => 1 );

    my $cmd = qq<
export LD_LIBRARY_PATH=${prefix}/dist/NEUTRINO/bin:\${LD_LIBRARY_PATH:+:}\$LD_LIBRARY_PATH
 
cwd=\$(pwd)
cd "${tmpdir}"

test -d settings || cp -R ${prefix}/dist/NEUTRINO/settings .
${prefix}/dist/NEUTRINO/bin/musicXMLtoLabel ${score} ${out}/full.lab ${out}/mono.lab

cd \$cwd
>;

    system( qw(sh -c), $cmd );
}

main(@ARGV);

=head1 NAME

musicxml-to-label - A wrapper script for NEUTRINO's musicXMLToLabel command

=head1 SYNOPSIS

  $ music-to-xml --score </path/to/score.musicxml> --out </path/to/out/dir>

=head1 OPTIONS

=head2 -s, -x, --score or --musicxml

A file of musicxml file

=head2 -o, --out or --output

A output directory proceed files.

=head1 SEE ALSO

L<https://n3utrino.work>

L<https://github.com/nyarla/neutrino-helpers/>

=head1 AUTHOR OF THIS SCRIPT

OKAMURA Naoki aka nyarla E<lt>nyarla@kalaclista.comE<gt>

=head1 LICENSE

This script is same license of Perl itself.

=cut
