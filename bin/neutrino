#!/usr/bin/env perl

use strict;
use warnings;
use utf8;

our $VERSION = "v0.1.0";

use Getopt::Long qw( :config posix_default no_ignore_case bundling auto_help );
use Pod::Usage qw( pod2usage );
use File::Temp qw( tempdir );

my $prefix = $ENV{'NEUTRINO_PREFIX'} // $ENV{'HOME'} . '/.local/share/neutrino';
my $nproc = `nproc --all --ignore 1`;
chomp($nproc);

sub err($) {
    my $msg = shift;
    print STDERR ( $msg, "\n" );
}

sub main {
    local @ARGV = @_;

    if ( @ARGV == 0 ) {
        pod2usage( -exitval => 1, -verbose => 1 );
    }

    my ( $label, $timing, $out, $model, $threads, $shift );

    GetOptions(
        "label|l=s"      => \$label,
        "timing|t=s"     => \$timing,
        "out|output|o=s" => \$out,
        "model|m=s"      => \$model,
        "threads|p=i"    => \$threads,
        "styleshift|s=i" => \$shift,
        "help|h"         => sub { pod2usage( -exitval => 0, -verbose => 1 ); },
        "version|v"      =>
          sub { print( ( split q{/}, $0 )[-1], " - ", $VERSION, "\n" ); exit 0 }
        ,
    ) or pod2usage( -exitval => 1, -verbose => 1 );

    if ( !-f $label ) {
          err "label file is missing: ${label}";
        exit 1;
    }

    if ( !-d $out && -e $out ) {
          err "output directory already exists as file: ${out}";
        exit 1;
    }

    if ( !-d "${prefix}/dist/NEUTRINO/model/${model}" ) {
          err "missing models files: ${model}";
        exit 1;
    }

    $threads //= $nproc;
    $shift   //= 1;

    my $tmpdir = tempdir( CLEANUP => 1 );

    my $cmd = <<"...";
export LD_LIBRARY_PATH=${prefix}/dist/NEUTRINO/bin:\${LD_LIBRARY_PATH:+:}\$LD_LIBRARY_PATH

cwd=\$(pwd)
cd "${tmpdir}"

test -d settings || cp -R ${prefix}/dist/NEUTRINO/settings .
${prefix}/dist/NEUTRINO/bin/NEUTRINO \\
  ${label} ${timing} \\
  ${out}/${model}.f0 ${out}/${model}.mgc ${out}/${model}.bap \\
  ${prefix}/dist/NEUTRINO/model/${model}/ \\
  -n ${threads} -k ${shift} -t -m

cd \$cwd
...

    system( qw(sh -c), $cmd );
}

main(@ARGV);

=head1 NAME

neutrino - A wrapper script for L<https://n3utrino.work>'s NEUTRINO command

=head1 SYNOPSIS

  $ neutrino -l <label.lab> -t <timing.lab> -o out -m MERROW -p 32 -s 1

=head1 OPTIONS

=head2 -l or --label

A file of NEUTRINO's label file.

=head2 -t or --timing

A file of NEUTRINO 's timing file

=head2 -o, --out or --output

Output directory of proceed files.

=head2 -m or --model

The name of NEUTRINO's model

=head2 -s or --styleshift

Set parameter of NEUTRINO's neutrino command.

=head2 -p or --threads

The number of threads use.

=head1 SEE ALSO

L<https://n3utrino.work>

L<https://github.com/nyarla/neutrino-helpers/>

=head1 AUTHOR OF THIS SCRIPT

OKAMURA Naoki aka nyarla E<lt>nyarla@kalaclista.comE<gt>

=head1 LICENSE

This script is same license of Perl itself.

=cut
